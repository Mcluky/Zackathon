plugins {
    id 'org.springframework.boot' version '2.5.4'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id "org.mikeneck.graalvm-native-image" version "1.4.1"
    id 'com.google.cloud.tools.jib' version '3.1.4'
}

group = 'ch.zuehlke.fullstack'
version = '1.0.0'
sourceCompatibility = '16'

bootJar {
    into('static') {
        from('../frontend/dist/frontend/')
    }
}
bootJar.dependsOn ":frontend:npm_run_dist"



jib{
    from{
        image = 'openjdk:16'
        auth {
            username = providers.environmentVariable('DOCKERHUB_USER')
                    .forUseAtConfigurationTime()
                    .getOrElse(project.findProperty('dockerHubUser') as String ?: '')
            password = providers.environmentVariable('DOCKERHUB_PWD')
                    .forUseAtConfigurationTime()
                    .getOrElse(project.findProperty('dockerHubPwd') as String ?: '')
        }
    }
    to {
        auth {
            username = providers.environmentVariable('DOCKERHUB_USER')
                    .forUseAtConfigurationTime()
                    .getOrElse(project.findProperty('dockerHubUser') as String ?: '')
            password = providers.environmentVariable('DOCKERHUB_PWD')
                    .forUseAtConfigurationTime()
                    .getOrElse(project.findProperty('dockerHubPwd') as String ?: '')
        }
    }
    container {
        ports = ["80"]
    }
}



repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    implementation 'org.graalvm.js:js-scriptengine:21.1.0'
    implementation 'org.graalvm.js:js:21.1.0'

    implementation 'io.springfox:springfox-swagger2:2.9.2'
    implementation 'io.springfox:springfox-swagger-ui:2.9.2'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
}

test {
    useJUnitPlatform()
}